# Extract core SWD as skos thesaurus
#
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gndsc: <http://d-nb.info/standards/vocab/gnd/gnd-sc#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gnd: <http://d-nb.info/gnd/>
PREFIX stw: <http://zbw.eu/stw/descriptor/>
PREFIX ddc: <http://dewey.info/class/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
construct {

  # define gnd class as subclass of skos:Concept
  gndo:SubjectHeadingSensoStricto rdfs:subClassOf skos:Concept .

  # introduce a concept scheme
  <http://zbw.eu/beta/swdskos> a skos:ConceptScheme ;
    skos:prefLabel "GND Sachbegriffe als SKOS Thesaurus"@de .

  ?concept a skos:Concept, gndo:SubjectHeadingSensoStricto ;
    skos:inScheme <http://zbw.eu/beta/swdskos> ;

    # include all original data
    ?originalProperty ?originalObject ;

    # labels
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel ;
    skos:definition ?definition ;

    # defined skos relations
    skos:narrower ?narrowerConcept ;
    skos:broader ?broaderConcept ;
    skos:related ?relatedConcept ;

    # subject categories
    skos:broader ?subjectCategory ;

    # matches to other vocabs
    ?matchingProperty ?matchingConcept ;
  .

  # missing reverse relations
  ?broaderConcept skos:narrower ?concept .
  ?relatedConcept skos:related ?concept .
  ?subjectCategory skos:narrower ?concept .
}
where {
  # restrict to example concept, or 'undef' for all
  values ( ?concept )
  {
    ( <http://d-nb.info/gnd/4056366-2> )
    ##( undef )
  }
  ?concept a gndo:SubjectHeadingSensoStricto ;
           ?originalProperty ?originalObject ;
           gndo:preferredNameForTheSubjectHeading ?prefPlain .

  # add language tag
  bind(strlang(?prefPlain, "de") as ?prefLabel)

  optional {
    ?concept gndo:variantNameForTheSubjectHeading ?altPlain
    bind(strlang(?altPlain, "de") as ?altLabel)
  }
  optional {
    ?concept gndo:definition ?defPlain
    bind(strlang(?defPlain, "de") as ?definition)
  }

  # dump down different kinds of broader/narrower links
  optional {
    ?concept gndo:broaderTerm|gndo:broaderTermGeneral|gndo:broaderTermGeneric|gndo:broaderTermInstantial|gndo:broaderTermPartitive ?broaderConcept .
  }
  optional {
    ?concept gndo:narrowerTerm|gndo:narrowerTermGeneral|gndo:narrowerTermGeneric|gndo:narrowerTermInstantial|gndo:narrowerTermPartitive ?narrowerConcept .
  }
  optional {
    ?concept gndo:relatedTerm ?relatedConcept .
  }

  # matches to other vocabs
  optional {
    ?concept skos:exactMatch|skos:closeMatch|skos:relatedMatch|skos:broadMatch|skos:narrowMatch ?matchingConcept .
    ?concept ?matchingProperty ?matchingConcept .
  }

  # ddc exact matches
##  optional {
##    ?concept gndo:relatedDdcWithDegreeOfDeterminacy4 ?emConcept
##  }

  # dump down ddc close matches (skip very loose matching concepts)
##  optional {
##    ?concept gndo:relatedDdcWithDegreeOfDeterminacy3|gndo:relatedDdcWithDegreeOfDeterminacy2 ?emConcept
##  }

  optional {
    ?concept gndo:gndSubjectCategory  ?subjectCategory
  }
}

