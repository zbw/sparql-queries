# Prominent economists, as indicated by a certain property,
# lacking another propery (e.g., RePEc authors without a GND ID)
#
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select ?wd ?wdLabel
(group_concat(distinct str(?life) ; separator = "; ") as ?wdLife)
?wdDescription
(group_concat(distinct str(?affiliation) ; separator = "; ") as ?wdAffiliations)
?lookup ('look up in authority' as ?lookupLabel)
where {
  # configuration of the query:
  #   ?missingProperty - the wikidata authority property to add
  #   ?definingPropterty - the wikidata authority property which indicates
  #       relevance and defines the restricted list of items
  #   ?lookupURL - a URL fragment which can be used to search names in
  #       the authority file addressed by ?missingProperty
  #       (this can address an arbitrary web application)
  # e.g., GND missing for TED speakers, lookup in SPARQL Lab GND endpoint
  values ( ?missingProperty ?definingProperty ?lookupURL ) {
    ( wdt:P227 wdt:P2428 'http://zbw.eu/beta/sparql-lab/?endpoint=http://zbw.eu/beta/sparql/gnd/query&queryRef=https://api.github.com/repos/jneubert/sparql-queries/contents/gnd/search_person.rq&name=' )
  }
  # restrict the data to "occupation economist"
  ##?wd wdt:P106 wd:Q188094 .
  #
  # restrict to items with the property indicating relevance
  ?wd ?definingProperty [] .
  #
  # restrict to items without the missing property
  filter(not exists {
      ?wd ?missingProperty [] .
    })
  #
  # not all items have labels in English
  optional {
    ?wd rdfs:label ?wdLabelLang
    filter(lang(?wdLabelLang) = 'en')
    bind(str(?wdLabelLang) as ?wdLabel)
  }
  # additional information for identifying the person
  optional {
    ?wd wdt:P569 ?birthDate .
  }
  optional {
    ?wd wdt:P570 ?deathDate .
  }
  bind(concat(coalesce(str(year(?birthDate)), ''), coalesce(concat(' - ', str(year(?deathDate))), '')) as ?life)
  #
  optional {
    ?wd schema:description ?descriptionLang
    filter(lang(?descriptionLang) = 'en')
    bind(str(?descriptionLang) as ?wdDescription)
  }
  optional {
    ?wd wdt:P108/rdfs:label ?affiliationLang .
    filter(lang(?affiliationLang) = 'en')
    bind(str(?affiliationLang) as ?affiliation)
  }
  # build lookup link
  bind(encode_for_uri(?wdLabel) as ?searchedName)
  bind(uri(concat(?lookupURL, ?searchedName)) as ?lookup)
  #
  # get site links (for sorting according to prominence)
  ?sitelink schema:about ?wd .
  # filter out wikicite/wikicommons etc. links
  filter(contains(str(?sitelink), 'wikipedia.org/'))
}
group by ?wd ?wdLabel ?wdDescription ?lookup
order by desc(count(distinct ?sitelink))

