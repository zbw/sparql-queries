# Get mappings for all categories (geo/subject/ware)
#
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#
construct {
  ?item skos:exactMatch ?pm20Category .
}
##select ?pm20Category ?item ?itemLabel
where {
  {
    # for wares, a few special pm20 category items plus "normal" items are matched
    service <https://query.wikidata.org/sparql> {
      ?item wdt:P10890 ?wareId .
    }
    bind (uri(concat('https://pm20.zbw.eu/category/ware/i/', ?wareId)) as ?pm20Category)
  } union {
    # for countries, only "normal" items (a few non-exact matches)
    service <https://query.wikidata.org/sparql> {
      ## TODO only exact matches?
      ?item wdt:P8483 ?geoCode .
    }
    # look up code (= notation)
    bind(replace(?geoCode, '_', ' ') as ?notation)
    ?geo skos:notation ?notation .
    bind(?geo as ?pm20Category)
  } union {
    # for subjects, only special pm20 category items are matched
    service <https://query.wikidata.org/sparql> {
      ?item wdt:P8484 ?subjectCode .
    }
    # look up code (= notation)
    bind(replace(?subjectCode, '_', ' ') as ?notation)
    ?subject skos:notation ?notation .
    bind(?subject as ?pm20Category)
  }
}
order by ?pm20Category

