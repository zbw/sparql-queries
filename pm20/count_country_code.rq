# Count folders by ISO 3166-1 alpha-2 country code
# (currently only applies to CompanyFolder)
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX frapo: <http://purl.org/cerif/frapo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?count (str(?cnt) as ?countLabel) ?code ?country
where {
  { 
    select (count(distinct ?pm20) as ?cnt) ?code
    where {  
      ?pm20 a zbwext:Pm20Folder ;
            frapo:hasCountryCode ?code .
      #
      # restrict to folders with online documents
      ?pm20 zbwext:freeDocCount [] .
    }
    group by ?code
  }
  #
  # add country label from Wikidata
  service <https://query.wikidata.org/sparql> {
    values ( ?language ) {
      ( "de" )
    }
    ?wd wdt:P297 ?code ;
        rdfs:label ?labelLang .
    filter(lang(?labelLang) = ?language)
  }
  bind(str(?labelLang) as ?country)
  #
  bind(uri(concat('http://zbw.eu/beta/sparql-lab/?endpoint=http://zbw.eu/beta/sparql/pm20/query',
        '&queryRef=https://api.github.com/repos/zbw/sparql-queries/contents/pm20/folder_by_country_code.rq',
        '&language=', ?language, '&code=', ?code , '&country=', ?country)) as ?count)
}
order by desc(?cnt)


